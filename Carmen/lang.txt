# Whitespace is required by parser.
# Parsed tokens will be combined into single tokens for AST parsing.

# variables are explicitly code block local only accessile to code within their block and sub blocks except for global variables defined prior to entry point.
# variable shadowing is not permitted

# Math Precedence Table
# 1. PARENTHESES
# 2. INCREMENT/DECREMENT
# 3. POWER
# 4. MULTIPLICATION/DIVISION/MODULUS
# 5. ADDITION/SUBTRACTION
# 6. COMPARISON
# 7. LOGICAL (NOT -> AND -> OR/XOR)

VARIABLE_TYPES --> ('nullable' | 'pointer to')? ('bool'('s')? | 'integer'('s')? | 'single'('s')? | 'double'('s')? | 'char'('s')? | 'decimal'('s')?
				| 'string'('s')? | 'short'('s')? | 'long'('s')? | 'void'('s')? 
				| STRUCTURE_ID | ARRAY_TYPE)
ARRAY_TYPE --> 'array' ('with size' ARRAY_SIZE)? 'of' VARIABLE_TYPES
ARRAY_SIZE --> EXPRESSION ('by' EXPRESSION)*
			 #| 'array of' #COUNT EXPRESSION# VARIABLE_TYPES
             #| EXPRESSION VARIABLE_TYPES
			 # ^^ Can't use this to parse complexity of multidimensional arrays/type collision
             # 'size' EXPRESSION ('by' EXPRESSION)* <-- look for 'by' ? << multidimentional

EXPRESSION --> IDENTIFIER | STRINGLITERAL | NUMBERLITERAL | BOOLEANLITERAL | NULLLITERAL | ARRAYLITERAL | CHARLITERAL
			| MATH_OPERATION | LOGICAL_OPERATION | COMPARISON_OPERATION | ARRAY_ACCESS
			| BIT_SHIFT_OPERATION | BITWISE_OPERATION | NUL_COALESCE_OPERATION | FUNCTION_CALL | CONCAT_OPERATION | TERNARY_OPERATION
			| PARENTHISED_EXPRESSION | BIT_ROTATION_OPERATION | ARRAY_SLICE | ARRAY_STRIDE | NULL_CHECK | TYPE_CAST | ADDRESS_OF
			| TYPE_CHECK

ADDRESS_OF --> 'the address of' EXPRESSION # Returns the address of the expression, which can be used for pointer operations.

PARENTHISED_EXPRESSION --> '(' EXPRESSION ')'

IDENTIFIER --> VARIABLE_IDENTIFIER # | FUNCTION_ID | STRUCTURE_ID | LABEL_ID | ALIAS_ID # Added alias as a valid identifier / containing the identifier.
VARIABLE_IDENTIFIER --> VARIABLE_ID | ARRAY_ACCESS | MEMBER_ACCESS # Added array access as a valid identifier / containing the identifier.
VARIABLE_ID --> #[a-zA-Z0-9_]* # Current parser no longer needs prefix
# FUNCTION_ID --> "@"[a-zA-Z_]*        #[a-zA-Z0-9_]*
# STRUCTURE_ID --> "#"[a-zA-Z_]*       #[a-zA-Z0-9_]*
# LABEL_ID --> ":"[a-zA-Z_]*           #[a-zA-Z0-9_]*
# ALIAS_ID --> "_"[a-zA-Z_]*           #[a-zA-Z0-9_]*

# Support escape sequences '\' in string and char literals
STRINGLITERAL --> '"' [^"]* '"'
NUMBERLITERAL --> [0-9]+ ('.' [0-9]+)? | 0x[0-9a-fA-F]+ | 0b[01]+ | 0o[0-7]+
BOOLEANLITERAL --> 'true' | 'false'
NULLLITERAL --> 'null'
CHARLITERAL --> "'" ( [^'\\] | '\\' [nrt'"\\] ) "'"
ARRAYLITERAL --> ';' (EXPRESSION (',' EXPRESSION)*)?  # List terminated by logic EOS, parenthesized, to level keyword parse
ARRAY_ACCESS --> 'index' EXPRESSION 'of' EXPRESSION
#              | 'the' INDEX_EXPRESSION 'of' EXPRESSION
#              | 'at index' expression
# INDEX_EXPRESSION --> EXPRESSION 'index' ('from last')? | 'index' EXPRESSION ('from last')? | 'initial index' | 'final index'

# Operations listed in precedence.
MATH_OPERATION --> INCREMENT | DECREMENT | MODULUS | POWER | MULTIPLICATION | DIVISION | ADDITION | SUBTRACTION | ROOT

ADDITION --> ('the sum of' EXPRESSION 'and' EXPRESSION) | (EXPRESSION ('plus'|'+') EXPRESSION)
SUBTRACTION --> ('the difference of' EXPRESSION 'and' EXPRESSION) | (EXPRESSION ('minus'|'-') EXPRESSION)
MULTIPLICATION --> ('the product of' EXPRESSION 'and' EXPRESSION) | (EXPRESSION ('times'|'*') EXPRESSION)
DIVISION --> ('the quotient of' EXPRESSION 'and' EXPRESSION) | (EXPRESSION ('divided by'|'/') EXPRESSION)
MODULUS --> ('the modulo of' EXPRESSION 'and' EXPRESSION) | (EXPRESSION '%' EXPRESSION)
POWER --> (EXPRESSION 'raised to the power of' EXPRESSION)
ROOT --> (EXPRESSION 'root of' EXPRESSION)



INCREMENT --> INCREMENT_PRE | INCREMENT_POST
INCREMENT_PRE --> ('increment' EXPRESSION)
INCREMENT_POST --> (EXPRESSION 'increment')		
DECREMENT --> DECREMENT_PRE | DECREMENT_POST
DECREMENT_PRE --> ('decrement' EXPRESSION)
DECREMENT_POST --> (EXPRESSION 'decrement')	

COMPARISON_OPERATION --> EQUALS | NOT_EQUALS | GREATER_THAN | LESS_THAN | GREATER_THAN_OR_EQUAL_TO | LESS_THAN_OR_EQUAL_TOe or' EXPRESSION

EQUALS --> EXPRESSION 'equal to' EXPRESSION
NOT_EQUALS --> EXPRESSION 'not equal to' EXPRESSION
GREATER_THAN --> EXPRESSION 'greater than' EXPRESSION	| EXPRESSION 'not less than or equal to' EXPRESSION		# this one is a cool byproduct of the parser
LESS_THAN --> EXPRESSION 'less than' EXPRESSION			| EXPRESSION 'not greater than or equal to' EXPRESSION	# as is this one
GREATER_THAN_OR_EQUAL_TO --> EXPRESSION 'not less than' EXPRESSION | EXPRESSION 'greater than or equal to' EXPRESSION
LESS_THAN_OR_EQUAL_TO --> EXPRESSION 'not greater than' EXPRESSION | EXPRESSION 'less than or equal to' EXPRESSION


LOGICAL_OPERATION --> AND | OR | NOT | XOR

NOT --> 'not' EXPRESSION														# Process not after other logical operations to prevent collisions
AND --> EXPRESSION 'and' EXPRESSION
OR --> EXPRESSION 'or' EXPRESSION
XOR --> EXPRESSION 'xor' EXPRESSION | EXPRESSION 'exclusiv
TYPE_CHECK --> EXPRESSION 'is' ('not')? 'a' EXPRESSION						# first expression resolves to data point, second expression resolves to type
NULL_CHECK --> EXPRESSION 'is null' | EXPRESSION 'is not null'              # essentially is a type check call.

BIT_SHIFT_OPERATION --> LEFT_SHIFT | RIGHT_SHIFT
LEFT_SHIFT --> EXPRESSION 'shifted left by' EXPRESSION
RIGHT_SHIFT --> EXPRESSION 'shifted right by' EXPRESSION

BIT_ROTATION_OPERATION --> LEFT_ROTATE | RIGHT_ROTATE
LEFT_ROTATE --> EXPRESSION 'rotated left by' EXPRESSION
RIGHT_ROTATE --> EXPRESSION 'rotated right by' EXPRESSION

BITWISE_OPERATION --> BITWISE_AND | BITWISE_OR | BITWISE_XOR | BITWISE_NOT
BITWISE_AND --> EXPRESSION 'bitwise and' EXPRESSION
BITWISE_OR --> EXPRESSION 'bitwise or' EXPRESSION
BITWISE_XOR --> EXPRESSION 'bitwise xor' EXPRESSION
BITWISE_NOT --> 'bitwise not' EXPRESSION

NUL_COALESCE_OPERATION --> EXPRESSION ', if not null, otherwise' EXPRESSION

CONCAT_OPERATION --> EXPRESSION 'concatenated with' EXPRESSION

MEMBER_ACCESS -->  EXPRESSION ''s' EXPRESSION # person''s' name





ARRAY_SLICE --> 'elements' ('from' EXPRESSION)? ('until' EXPRESSION)? 'of' EXPRESSION
ARRAY_STRIDE --> 'every' EXPRESSION 'element of' EXPRESSION
TERNARY_OPERATION --> 'if' EXPRESSION 'then' EXPRESSION 'otherwise' EXPRESSION


# safe cast will return nullable
TYPE_CAST --> ('safe' | 'unsafe')? 'cast' EXPRESSION 'as a' EXPRESSION	# first expression resolves to data point, second expression resolves to type

FUNCTION_CALL --> 'call' FUNCTION_ID ('with' (EXPRESSION | ARRAYLITERAL))?
FUNCTION_PARAMETER -->
    'define' VARIABLE_ID 'as' 
    ('out' | 'in')? # read write 'ref' unnecesary due to pointer use
    ('restrict')? # promise
    (
        ('nullable')? VARIABLE_TYPES ('equal to' EXPRESSION)?
        | ('restrict')? 'pointer to' VARIABLE_TYPES ('equal to' EXPRESSION)? 
    )




PROGRAM --> (STATEMENT '.')*

STATEMENT --> STATEMENT_BLOCK | ASSIGNMENT | VARIABLE_DEFINITION | CONDITIONAL_STATEMENT | FUNCTION_CALL_STMT | LABEL_STATEMENT | GOTO_STATEMENT | WHILE_LOOP
			| DO_WHILE_LOOP | FOR_LOOP | FOREACH_LOOP | BREAK_STATEMENT | CONTINUE_STATEMENT | ITERATOR_LOOP | ASSERTION_STATEMENT | STRUCTURE_DEFINITION | FUNCTION_DEFINITION
			| RETURN_STATEMENT | USING_STATEMENT | IMPORT_STATEMENT | ASM_STATEMENT | COMPOUND_ASSIGNMENT
# Statements are separated by a period, but the last statement does not require a period.	

ASM_STATEMENT --> 'asm' ('unsafe')? (ARCHITECTURE_MODIFIER)? 
                  ('clobbers' STRINGLITERAL)? 
                  STRINGLITERAL. <-- containing asm code for lexer simplicity

ARCHITECTURE_MODIFIER --> '16bit' | '32bit' | '64bit'

COMPOUND_ASSIGNMENT --> 
    ('add' EXPRESSION 'to' VARIABLE_IDENTIFIER) |
    ('subtract' EXPRESSION 'from' VARIABLE_IDENTIFIER) |
    ('multiply' VARIABLE_IDENTIFIER 'by' EXPRESSION) |
    ('divide' VARIABLE_IDENTIFIER 'by' EXPRESSION) |
    ('modulate' VARIABLE_IDENTIFIER 'with' EXPRESSION)

RETURN_STATEMENT --> 'return' EXPRESSION?

LABEL_STATEMENT --> 'label' LABEL_ID
GOTO_STATEMENT --> 'goto' LABEL_ID

ASSERTION_STATEMENT --> 'assert' CONDITION (',' STRINGLITERAL)?

BREAK_STATEMENT --> 'break'
CONTINUE_STATEMENT --> 'continue'

STATEMENT_BLOCK --> 'execute the following;' (STATEMENT)* 'fin'

#parser will simply check if the entire statement validates as a function call
EXPRESSION_STMT --> FUNCTION_CALL | INCREMENT_STMT | DECREMENT_STMT
FUNCTION_CALL_STMT --> FUNCTION_CALL # function call as a statement for when no return or throwaway is expected.
INCREMENT_STMT --> INCREMENT_PRE | INCREMENT_POST
DECREMENT_STMT --> DECREMENT_PRE | DECREMENT_POST

ASSIGNMENT --> 'set' VARIABLE_IDENTIFIER 'equal to' EXPRESSION
             | 'assign' VARIABLE_IDENTIFIER ', ' EXPRESSION


USING_STATEMENT --> 'using' VARIABLE_ID 'as' EXPRESSION # cleanup when done
IMPORT_STATEMENT --> 'import' STRINGLITERAL ('as' IDENTIFIER)? # 
ALIAS_STATEMENT --> 'alias' ALIAS_ID 'as' EXPRESSION # alias is a reference to an existing accessable variable, not a copy. It can be used to create a new name for an existing variable.

ITERATOR_LOOP --> 'iterate over' EXPRESSION
				('with index as' VARIABLE_ID)? 
                ('with value as' VARIABLE_ID)? 
				 'do' STATEMENT # while in this loop provide 'index' and 'item' as keyword values. // compiler can ignore unused values in future.
FOREACH_LOOP --> 'for each' VARIABLE_ID 'in' EXPRESSION 'do' STATEMENT
FOR_LOOP --> 'for' VARIABLE_ID 'from' EXPRESSION 'to' EXPRESSION ('step' EXPRESSION)? 'do' STATEMENT

WHILE_LOOP --> 'while' EXPRESSION 'do' STATEMENT
DO_WHILE_LOOP --> 'do' STATEMENT 'while' EXPRESSION

# if i mandate expression is parenthised or statement is a block the then keyword is reasonably unnecessary
# Parser will initially parse if/elif/else as separate statements >> Validator will move elifs and else to inner if
# ex parser find if >> look for then ? parse : check paren | block >> parse
CONDITIONAL_STATEMENT --> 'if' EXPRESSION 'then' STATEMENT ('otherwise if' EXPRESSION 'then' STATEMENT)?+ ('otherwise' STATEMENT)?

STRUCTURE_DEFINITION --> 'define structure' ('template')? STRUCTURE_ID 
                            (STRUCTURE_INHERITANCE)? 
                            (TEMPLATE_TYPES)? 
                            'with:' BLOCK_STATEMENT <-- contining vars and funcs

STRUCTURE_INHERITANCE --> 'inheriting' TYPE #Yes i see the keyword reuse
TEMPLATE_TYPES --> 'for types;' TYPE_RESTRICTION (',' TYPE_RESTRICTION)*

TYPE_RESTRICTION --> T 'inheriting' T ('and' T)*

FUNCTION_DEFINITION --> 
    'define function' ('template')? FUNCTION_ID 
    ('for' STRUCTURE_ID)? 
    (TEMPLATE_TYPES)?
    ('returning' VARIABLE_TYPES)?
    ('with' FUNCTION_PARAMETERS)? # possible no parameters <--- Can use containing structure generics
    ':' STATEMENT # either single statement or a block statement
FUNCTION_PARAMETERS --> ARRAYLITERAL << Of FUNCTION_PARAMETER
                      | FUNCTION_PARAMETER

# define name as number of type equal to value // array of // empty array
# Variable cannot be const and nullable it does not make sense. Constant definition must set a value.
# nullable types contain a default value of null, which is not the same as a constant.
# Numeric literals will default to integer or double (without flags), char literals to char, string literals to string, and boolean literals to bool.
# define has preferred use outside of stucture and function definitions
VARIABLE_DEFINITION --> 'define' VARIABLE_ID 'is a' 
            ( 'constant' VARIABLE_TYPES 'equal to' EXPRESSION
            | ('static')? VARIABLE_TYPES 'equal to' EXPRESSION )

ENUM_DEFINITION --> 'define enum' STRUCT_ID 'with' NONKEYWORDSYMBOL, ... # values compared against keywords and literals during parse

SWITCH_STATEMENT -->
    'switch' EXPRESSION (CASE_CLAUSE)+ 'otherwise' ':' STATEMENT # Block mandatory?
CASE_CLAUSE --> 'case' PATTERN ('when' EXPRESSION)? ':' STATEMENT # Block mandatory?

PATTERN --> LITERAL_PATERN
            | ENUM_PATTERN
            | TYPE_PATTERN
            | WILDCARD_PATTERN

LITERAL_PATTERN --> NUMBERLITERAL | STRINGLITERAL | BOOLEANLITERAL      # existing literals
ENUM_PATTERN --> ENUM_ID ''s' ENUM_VALUE                                # ExprMemberAccess IDENTIFIER '.' IDENTIFIER
TYPE_PATTERN --> VARIABLE_TYPES VARIABLE_ID?                            # <-- Type check && cast
WILDCARD_PATTERN --> '_'                                                # <-- Wildcard Keyword

MATCH_EXPRESSION -->
    'match' EXPRESSION 'with ;'
    (PATTERN 'to' EXPRESSION ',')* # <-- because 'match pattern to expression.'
    ('otherwise' EXPRESSION)?

# The program is a single file with imports referencing single files only structs and functions will be imported maintaining scope
# internal & private not visible to program file put still compiled properly

# 

# definitions structs, funcs, global vars and globals are at top of file followed by a program start label 'program:' or the like
# followed by the execution

▼ Concept general program structure ▼

import "anyfile.scroll".

define structure #point with:
  definitions.
eb

define function @getX for #point with:
  statements.
eb

define constant $counter equal to 0.

program:
  statements.
eb

▼ Concept structure for boot program writing with asm writer/compiler ▼

# MODE 16.
16 bit file imports
16 bit instruction set variables/definitions

# MODE 32.
32 bit file imports
32 bit instruction set variables/definitions

# MODE 64.
64 bit file imports
64 bit instruction set variables/definitions

program:
  # MODE 16
  16 bit run
  # MODE 32
  32 bit run
  # MODE 64
  64 bit run
eb
